package comdlg32_example

import runtime "base:runtime"
import strings "core:strings"
import log "core:log"
import win "core:sys/windows"

file_dialog_mode :: enum {
	open,
	save,
}

file_dialog_filter :: struct {
	// The full line of text displayed to the user in the selection box.
	//
	// Examples:
	//  - `"Text files (*.txt)"`
	//  - `"All files (*.*)"`
	display_text:  string,

	// The internal search pattern used to filter the files.
	//
	// Examples:
	//  - `"*.txt"`
	//  - `"*.*"` // NOTE: This includes files without an extension.
	filter:        string,
}

file_dialog :: proc(
	owner:                           win.HWND,
	mode:                            file_dialog_mode,
	initial_file:                    Maybe(string) = nil,
	filters:                         []file_dialog_filter = nil,
	#any_int selected_filter_index:  u32 = 0,
	dialog_caption:                  Maybe(string) = nil,
	initial_directory:               Maybe(string) = nil,
	allocator:                       runtime.Allocator = context.temp_allocator,
) -> (
	dialog_confirmed:                bool,
	selected_file:                   string,
) {
	file_buf := make([]u16, win.MAX_PATH_WIDE, context.temp_allocator)
	file_buf[0] = 0

	if initial_file != nil {
		converted := win.utf8_to_wstring(file_buf, initial_file.?)
		if converted == nil {
			// Conversion error.
			log.warnf("Unable to convert initial file name string.")
			file_buf[0] = 0
		}
	}

	ofn := win.OPENFILENAMEW {
		lStructSize = size_of(win.OPENFILENAMEW),
		hwndOwner = owner,
		lpstrFilter = file_dialog_build_filter_string(filters),
		nFilterIndex = selected_filter_index + 1,
		lpstrFile = raw_data(file_buf[:]),
		nMaxFile = win.MAX_PATH_WIDE,
	}

	if dialog_caption != nil {
		ofn.lpstrTitle = win.utf8_to_wstring(dialog_caption.?, context.temp_allocator)
	}
	if initial_directory != nil {
		ofn.lpstrInitialDir = win.utf8_to_wstring(initial_directory.?, context.temp_allocator)
	}

	confirmed: win.BOOL

	switch mode {
	case .open:
		ofn.Flags = win.OFN_PATHMUSTEXIST | win.OFN_FILEMUSTEXIST | win.OFN_EXPLORER
		confirmed = win.GetOpenFileNameW(&ofn)
	case .save:
		ofn.Flags = win.OFN_OVERWRITEPROMPT | win.OFN_EXPLORER
		confirmed = win.GetSaveFileNameW(&ofn)
	case:
		unreachable()
	}

	if confirmed {
		// Convert result to string.
		path := cast(cstring16)ofn.lpstrFile
		path_str, err1 := win.wstring_to_utf8(path, allocator = allocator)
		if err1 != nil {
			log.warnf("File dialog confirmed - string conversion error: %v", err1)
			return false, ""
		}
		return true, path_str
	} else {
		return false, ""
	}
}

// Converts the `filters` slice to the string format required by `OPENFILENAMEW.lpstrFilter`.
//
// Returns `nil` if `filters` is an empty slice.
file_dialog_build_filter_string :: proc(filters: []file_dialog_filter, allocator: runtime.Allocator = context.temp_allocator) -> win.LPCWSTR {

	if len(filters) == 0 {
		return nil
	}

	needed_capacity := 0
	for f in filters {
		needed_capacity += len(f.display_text)
		needed_capacity += 1
		needed_capacity += len(f.filter)
		needed_capacity += 1
	}
	needed_capacity += 1

	sb: strings.Builder
	strings.builder_init_len_cap(&sb, 0, needed_capacity, context.temp_allocator)
	
	for f in filters {
		strings.write_string(&sb, f.display_text)
		strings.write_string(&sb, "\x00")
		strings.write_string(&sb, f.filter)
		strings.write_string(&sb, "\x00")
	}
	strings.write_string(&sb, "\x00")

	return win.utf8_to_wstring(strings.to_string(sb), allocator)
}

// Converts a file path or file name to the display name that the file explorer would
// display to the user. This takes into account system settings regarding file extensions.
get_file_display_name :: proc(file_path: string, allocator := context.temp_allocator) -> (result: string, ok: bool) #optional_ok {
	file_path_wstr := win.utf8_to_wstring(file_path, context.temp_allocator)
	needed := win.GetFileTitleW(file_path_wstr, nil, 0)
	if needed <= 0 {
		return "", false
	}
	buf := make([]u16, needed, context.temp_allocator)
	conversion_result := win.GetFileTitleW(file_path_wstr, raw_data(buf[:]), auto_cast len(buf))
	if conversion_result == 0 {
		// Success.
		r, err := win.wstring_to_utf8(cast(win.wstring)raw_data(buf[:]), allocator = allocator)
		if err != nil {
			return "", false
		} else {
			return r, true
		}
	} else {
		// Conversion failure.
		return "", false
	}
}
